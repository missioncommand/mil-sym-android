description = 'MIL-STD-2525D Symbology Renderer'

apply plugin: 'com.android.library'
apply from: "${rootDir}/gradle/android-support.gradle"


task javadoc(type: Javadoc, dependsOn: 'assemble') {
    source = android.sourceSets.main.java.srcDirs

    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))

    exclude 'armyc2/c5isr/geo/**'
    exclude 'armyc2/c5isr/graphics2d/**'
    exclude 'armyc2/c5isr/JavaLineArray/**'
    exclude 'armyc2/c5isr/JavaTacticalRenderer/**'
    exclude 'armyc2/c5isr/RenderMultipoints/**'
    exclude 'armyc2/c5isr/sun/**'
    exclude 'armyc2/c5isr/web/exceptions/**'
    exclude 'armyc2/c5isr/web/json/**'
    exclude 'armyc2/c5isr/web/render/utilities/**'
    exclude 'armyc2/c5isr/web/render/GeoPixelConversion.java'
    exclude 'armyc2/c5isr/web/render/MultiPointHandler.java'
    exclude 'armyc2/c5isr/web/render/PointConverter.java'
    exclude 'armyc2/c5isr/web/render/Shape3DHandler.java'
    exclude 'armyc2/c5isr/web/render/SymbolModifiers.java'
    exclude 'armyc2/c5isr/renderer/symbolpicker/Node.java'
    exclude 'armyc2/c5isr/renderer/symbolpicker/TreeManager.java'
    exclude 'armyc2/c5isr/renderer/tester/**'
    exclude 'armyc2/c5isr/renderer/IIconRenderer.java'
    exclude 'armyc2/c5isr/renderer/IconRenderer.java'
    exclude 'armyc2/c5isr/renderer/PatternFillRenderer.java'
    exclude 'armyc2/c5isr/renderer/SinglePointRenderer.java'
	exclude 'armyc2/c5isr/renderer/SinglePointSVGRenderer.java'
    exclude 'armyc2/c5isr/renderer/utilities/EntityCode.java'
    exclude 'armyc2/c5isr/renderer/utilities/ErrorLogger.java'
    exclude 'armyc2/c5isr/renderer/utilities/GeoPixelConversion3D.java'
    exclude 'armyc2/c5isr/renderer/utilities/IMultiPointRenderer.java'
    exclude 'armyc2/c5isr/renderer/utilities/IPointConversion.java'
    exclude 'armyc2/c5isr/renderer/utilities/LRUCache.java'
    exclude 'armyc2/c5isr/renderer/utilities/LRUEntry.java'
    exclude 'armyc2/c5isr/renderer/utilities/MeasureSVGs.java'
    exclude 'armyc2/c5isr/renderer/utilities/ModifierInfo.java'
    exclude 'armyc2/c5isr/renderer/utilities/PathUtilities.java'
    exclude 'armyc2/c5isr/renderer/utilities/PointConversion.java'
    exclude 'armyc2/c5isr/renderer/utilities/PointConversionDummy.java'
    exclude 'armyc2/c5isr/renderer/utilities/PointConverter3D.java'
    exclude 'armyc2/c5isr/renderer/utilities/RectUtilities.java'
    exclude 'armyc2/c5isr/renderer/utilities/RendererUtilities.java'
    exclude 'armyc2/c5isr/renderer/utilities/SettingsChangedEvent.java'
    exclude 'armyc2/c5isr/renderer/utilities/SettingsChangedEventListener.java'
    exclude 'armyc2/c5isr/renderer/utilities/SettingsEventListener.java'
    exclude 'armyc2/c5isr/renderer/utilities/ShapeUtilities.java'
    exclude 'armyc2/c5isr/renderer/utilities/SVGELookup.java'
    //exclude 'armyc2/c5isr/renderer/utilities/SVGInfo.java'
    exclude 'armyc2/c5isr/renderer/utilities/SVGLookup.java'
    exclude 'armyc2/c5isr/renderer/utilities/SVGPath.java'
	exclude 'armyc2/c5isr/renderer/utilities/Shape2SVG.java'
    exclude 'armyc2/c5isr/renderer/utilities/SymbolDraw.java'
    //exclude 'armyc2/c5isr/renderer/utilities/TextInfo.java'

    options {
        if (JavaVersion.current().isJava8Compatible()) { // disable lint for java8
            addStringOption('Xdoclint:none', '-quiet')
        }
        encoding    = "UTF-8"
        docEncoding = "UTF-8"
        charSet     = "UTF-8"
        linkSource  = true

        logging.captureStandardError LogLevel.INFO
        logging.captureStandardOutput LogLevel.INFO

        title  = "MIL-STD-2525 Android Symbology Renderer (${project.version})"
    }
}

task javadocJar(type: Jar, dependsOn: 'javadoc') {
    from javadoc.destinationDir
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
}


project.afterEvaluate {
    publishing.publications {
        maven(MavenPublication) {
            pom.packaging = "aar"

            artifact("${buildDir}/outputs/aar/${project.name}-release.aar")
            artifact("${buildDir}/intermediates/bundles/release/classes.jar")

            artifact sourceJar { classifier "sources" }
            artifact javadocJar { classifier "javadoc" }
            updatePom(pom, project)
        }
    }

    javadoc.classpath += files(android.libraryVariants.collect { variant ->
        variant.javaCompileProvider.get().classpath.files
    })
}


dependencies {
    implementation 'com.caverock:androidsvg-aar:1.4'
    implementation 'org.gavaghan:geodesy:1.1.3'
    implementation 'com.android.support:support-v4'
}


ext {
    //TODO: update if we push to open source again
    PUBLISH_GROUP_ID = 'armyc2.c5isr.renderer'
    PUBLISH_VERSION = '2.2.0'
    PUBLISH_ARTIFACT_ID = 'mil-sym-android-renderer'
}

apply from: "${rootProject.projectDir}/scripts/publish-module.gradle"


